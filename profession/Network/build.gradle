apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    def versionConfig = rootProject.extensions.getByName("ext")

    compileSdkVersion versionConfig.compileSdkVersion
    buildToolsVersion versionConfig.buildToolsVersion

    defaultConfig {
        minSdkVersion versionConfig.minSdkVersion
        targetSdkVersion versionConfig.targetSdkVersion
        versionCode 2010091
        versionName "1.2.0"
        testInstrumentationRunner versionConfig.testInstrumentationRunner
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //项目初始化引入的dependencies
    implementation supportDependencies.appcompat
    implementation supportDependencies.multidex
    //引入 rxJava
    implementation dataDependencies.rxJava2
    implementation dataDependencies.rxAndroid2
    //引入 retrofit
    implementation dataDependencies.retrofit
    implementation dataDependencies.retrofitConverterGson
    implementation dataDependencies.retrofitAdapterRxJava
    //引入 rxLifecycle
    implementation dataDependencies.rxlifecycle
    //引入 Gson
    implementation dataDependencies.gson
    //引入 缓存库
    implementation dataDependencies.disklrucache
    //引入 greenDao
    implementation dataDependencies.greendao
    //引入 log
    implementation utilDependencies.log
    //引入 string
    implementation utilDependencies.string
    //引入 Resource
    //引入 baseNetwork
    implementation functionDependencies.baseNetwork
    //引入 baseApplication
    implementation functionDependencies.baseApplication
    //引入 loadDialog 需要同时引入附属的resource
    implementation widgetDependencies.loadDialog
    implementation functionDependencies.resource
    //引入 sharedPreference
    implementation utilDependencies.sharedPreference
}

def siteUrl = 'https://github.com/tgcityPlum/MavenJcenter' // 项目主页
def gitUrl = 'https://github.com/tgcityPlum/MavenJcenter.git' // 项目的git地址
def repoName = 'profession' // 项目的名称
def moduleName = 'Network' // 项目的名称
group = 'com.tgcity.profession' // 所在组
version = "1.2.0" //aar的版本号

install {
    repositories.mavenInstaller {
// This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name moduleName
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0' // 开源协议名称
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt' // 协议地址
                    }
                }
                developers {
                    developer {
                        id 'tgcity' // 账号
                        name 'tgcity' // 名称
                        email 'tgcityPlum@gmail.com' // 邮箱地址
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
// 读取配置文件中的用户名和key
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = repoName // 你在bintray上创建的库的名称
        name = moduleName // 在jcenter中的项目名称
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}